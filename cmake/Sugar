# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

# On including this file:
# 1. sugar cmake modules paths added to cmake modules paths
# 2. all libraries root paths setted

# test GITENV_ROOT and SUGAR_ROOT, priority:
# 1. SUGAR_ROOT setted as cmake variable
# 2. this file loaded as third party component (from release package)
# 3. GITENV_ROOT setted as cmake variable
# 4. test GITENV_ROOT environment variable (usual development)

if(NOT DEFINED SUGAR_ROOT)
  set(
      _GITENV_ROOT_THIRD_PARTY
      "${CMAKE_CURRENT_LIST_DIR}/../../../third_party"
  )
  if(EXISTS "${_GITENV_ROOT_THIRD_PARTY}")
    set(GITENV_ROOT "${_GITENV_ROOT_THIRD_PARTY}")
  endif()

  if(NOT DEFINED GITENV_ROOT)
    set(GITENV_ROOT "$ENV{GITENV_ROOT}")
  endif()

  if("${GITENV_ROOT}" STREQUAL "")
    message(FATAL_ERROR "can't detect GITENV_ROOT")
  endif()

  # normalize abs path
  get_filename_component(GITENV_ROOT ${GITENV_ROOT} ABSOLUTE)

  if(NOT EXISTS "${GITENV_ROOT}")
    message(FATAL_ERROR "can't find '${GITENV_ROOT}' (loaded from GITENV_ROOT)")
  endif()

  message("GITENV_ROOT: ${GITENV_ROOT}")

  set(SUGAR_ROOT ${GITENV_ROOT}/sugar)
endif()

message("SUGAR_ROOT: ${SUGAR_ROOT}")

# test self
if(NOT EXISTS "${SUGAR_ROOT}/cmake/Sugar")
  message(FATAL_ERROR "Can't find Sugar components in: ${SUGAR_ROOT}")
endif()

# add cmake modules to search path
list(APPEND CMAKE_MODULE_PATH "${SUGAR_ROOT}/cmake/core")
list(APPEND CMAKE_MODULE_PATH "${SUGAR_ROOT}/cmake/find")
list(APPEND CMAKE_MODULE_PATH "${SUGAR_ROOT}/cmake/print")
list(APPEND CMAKE_MODULE_PATH "${SUGAR_ROOT}/cmake/utility")

include(sugar_add_this_to_sourcelist)
sugar_add_this_to_sourcelist()

# set other libraries root paths
include(sugar_setup_libraries_paths)
sugar_setup_libraries_paths()
